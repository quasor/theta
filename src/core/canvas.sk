class GlyphInstance {
	var glyph Glyph = null
	var offset = Vector.new
	var scale = 0.0
}

class Canvas :: Layout.Renderer {
	var _platform Platform
	var _context GPU.Context
	var _pool GPU.BufferPool
	var _smooth GPU.Smooth
	var _equationMaterial GPU.Material
	var _glyphMaterial GPU.Material
	var _textMaterial GPU.Material
	var _font Font
	var _screenQuad GPU.BufferArea
	var _color GPU.Texture
	var _target GPU.RenderTarget
	var _instances List<GlyphInstance> = []
	var _instanceBounds = RectBuilder.new
	var _usedInstances = 0
	var _isInvalid = true
	var _validFrames = 0
	var _cachedEquation Equation.Node = null
	var _compiler = Equation.GLSLCompiler.new

	# Viewport transform
	var _origin = Vector.new
	var _scale = 25600.0

	# Temporary objects to avoid GC
	var _transformA = Transform.new
	var _transformB = Transform.new

	def new(platform Platform, fontData DataArray) {
		_platform = platform
		_context = platform.context
		_pool = GPU.BufferPool.new(_context)
		_smooth = GPU.Smooth.new(_context, _pool)
		_glyphMaterial = _context.createMaterial(Glyph.VERTEX_FORMAT, GLSLX_SOURCE_GLYPH_VERTEX, GLSLX_SOURCE_GLYPH_FRAGMENT)
		_textMaterial = _context.createMaterial(SCREEN_QUAD_FORMAT, GLSLX_SOURCE_TEXT_VERTEX, GLSLX_SOURCE_TEXT_FRAGMENT)
		_font = Font.new(fontData, _pool)
		_screenQuad = _pool.allocate(SCREEN_QUAD_FORMAT, DataArray.new.appendFloats([-1, -1, 1, -1, -1, 1, 1, 1]).bytes)
		_color = _context.createTexture(.NEAREST_CLAMP, 0, 0)
		_target = _context.createRenderTarget(_color)
	}

	def originX double {
		return _origin.x
	}

	def originY double {
		return _origin.y
	}

	def scale double {
		return _scale
	}

	def setOriginAndScale(originX double, originY double, scale double) {
		_origin.x = originX
		_origin.y = originY
		_scale = scale
	}

	def translateOrigin(deltaX double, deltaY double) {
		_origin.x += deltaX
		_origin.y += deltaY
	}

	def adjustScaleWithPin(x double, y double, ratio double) {
		_scale *= ratio
		if _scale < 2 {
			_scale = 2
			return
		}
		if _scale > 25600 {
			_scale = 25600
			return
		}
		var fs = Math.floor(_scale) as int
		console.log("Scale: ", fs)
		# _origin.x += (x - _origin.x) * (1 - ratio)
		# _origin.y += (y - _origin.y) * (1 - ratio)
	}

	def beginFrame {
		if _color.width != _platform.pixelWidth || _color.height != _platform.pixelHeight {
			_color.resize(_platform.pixelWidth, _platform.pixelHeight, null)
		}

		_transformA.reset
		_transformA.translate(-1, 1)
		_transformA.scale(2.0 / _platform.width, -2.0 / _platform.height)
		_smooth.setTransformAndPixelScale(_transformA, 1 / _platform.density)

		_context.beginFrame
		_context.setViewport(0, 0, _platform.pixelWidth, _platform.pixelHeight)
		_context.setCopyBlendState
		_context.clear(.WHITE)
	}

	def endFrame {
		_context.endFrame
	}

	def glyph(codePoint int, prefer Prefer) Glyph {
		return _font.glyph(codePoint, prefer)
	}

	def addGlyph(glyph Glyph, x double, y double, scale double) {
		if glyph.area == null {
			return # Ignore spaces
		}

		# Grow the glyph instance array if it's not big enough
		if _usedInstances == _instances.count {
			_instances.append(GlyphInstance.new)
		}

		# Place an instance of this glyph
		var instance = _instances[_usedInstances]
		instance.glyph = glyph
		instance.offset.set(x, y)
		instance.scale = scale
		_usedInstances++
		assert(_usedInstances <= _instances.count)

		# Expand the rendered bounds to include the instance
		var bounds = glyph.bounds
		var left = x + bounds.x * scale
		var top = y + bounds.y * scale
		var right = left + bounds.width * scale
		var bottom = top + bounds.height * scale
		_instanceBounds.include(left, top)
		_instanceBounds.include(right, top)
		_instanceBounds.include(left, bottom)
		_instanceBounds.include(right, bottom)
	}

	def addBox(x double, y double, width double, height double) {
		var compiler = _font.glyphCompiler
		var glyph = Glyph.new

		compiler.begin(glyph)
		compiler.moveTo(0, 0)
		compiler.lineTo(width, 0)
		compiler.lineTo(width, height)
		compiler.lineTo(0, height)
		compiler.end

		addGlyph(glyph, x, y, 1)
	}

	def addSelectionBox(x double, y double, width double, height double) {
		_smooth.fillRect(x, y, width, height, SELECTION_COLOR)
	}

	def addErrorLine(x1 double, x2 double, y double) {
		# Triangle
		if x1 == x2 {
			_smooth.fillConvexPolygon([
				Vector.new(x1, y - 1),
				Vector.new(x1 + 3, y + 3),
				Vector.new(x1 - 3, y + 3),
			], .RED)
		}

		# Squiggle
		else {
			var line List<Vector> = []
			var zigzag = -1
			for x = x1; x < x2; x += 2 {
				line.append(Vector.new(x, y + zigzag))
				zigzag = -zigzag
			}
			_smooth.strokeNonOverlappingPolyline(line, .RED, 1, .OPEN)
		}
	}

	def glyphCompiler GlyphCompiler {
		return _font.glyphCompiler
	}

	def lineHeight double {
		return _font.ascender - _font.descender
	}

	def _computeTextWidth(text string, scale double, prefer Prefer) double {
		var width = 0.0
		for i in 0..text.count {
			width += scale * _font.glyph(text[i], prefer).advanceWidth
		}
		return width
	}

	def addText(text string, x double, y double, scale double, prefer Prefer) {
		for i in 0..text.count {
			var glyph = _font.glyph(text[i], prefer)
			addGlyph(glyph, x, y, scale)
			x += scale * glyph.advanceWidth
		}
	}

	def _formatNumber(value double) string {
		var text string = (+(value as dynamic).toFixed(6)).toString()
		if text[0] == '-' {
			return "âˆ’" + text.slice(1)
		}
		return text
	}

	def drawGrid {
		var origin = _origin
		var scale = _scale
		var width = _platform.width
		var height = _platform.height
		var step = 100

		# Grid lines - one line per pixel starting at 800% (scale=800)
		if scale >= GRID_SHOW_LINES_AT_SCALE {

			var ratio = step / scale
			var left = Math.ceil(-origin.x * ratio) as int
			var top = Math.ceil((height - origin.y) * -ratio) as int
			var right = Math.ceil((width - origin.x) * ratio) as int
			var bottom = Math.ceil(-origin.y * -ratio) as int

			# Vertical axis
			for x in left..right {
				# console.log("R", x)
					var tx = origin.x + x * scale / step
					_smooth.strokeLineWithoutEndpointAA(tx, 0, tx, height, GRID_LIGHT_COLOR, 0.5)
			}

			# Horizontal axis
			for y in top..bottom {
					var ty = origin.y - y * scale / step
					_smooth.strokeLineWithoutEndpointAA(0, ty, width, ty, GRID_LIGHT_COLOR, 0.5)
			}
		}

		# Coordinate text and tick marks
		var log = Math.log(scale / 100) / Math.log(10)
		var intPart = Math.floor(log)
		var fractPart = log - intPart
		var textStep = Math.clamp(Math.pow(10, intPart) * (
			fractPart > Math.log(5) / Math.log(10) ? 5 :
			fractPart > Math.log(2) / Math.log(10) ? 2 :
			1), 0, step)
		var textRatio = textStep / scale

		var textLeft = Math.ceil((-100 - origin.x) * textRatio) as int
		var textTop = Math.ceil((height - origin.y) * -textRatio) as int
		var textRight = Math.ceil((width - origin.x) * textRatio) as int
		var textBottom = Math.ceil((-100 - origin.y) * -textRatio) as int

		# Horizontal axis
		var textY = 0 # Math.clamp(origin.y, 0, height - GRID_FONT_SIZE * 1.3)
		for x in textLeft..textRight {
			var tx = origin.x + x * scale / textStep
			_smooth.strokeLine(tx, 0, tx, 0 + GRID_TICK_SIZE, GRID_DARK_COLOR, 2)
			addText(_formatNumber(x / textStep * 100), tx + GRID_FONT_SIZE * 0.3, textY + GRID_FONT_SIZE * 0.1, GRID_FONT_SIZE, .PREFER_NORMAL)
		}

		# Vertical axis
		var textX = 0 # Math.max(origin.x, 0) + GRID_FONT_SIZE * 0.3
		for y in textTop..textBottom {
			var text = _formatNumber(y / textStep * 100)
			var ty = origin.y - y * scale / textStep
			_smooth.strokeLine(0, ty, 0 + GRID_TICK_SIZE, ty, GRID_DARK_COLOR, 2)
			addText(text, Math.min(textX, width - _computeTextWidth(text, GRID_FONT_SIZE, .PREFER_NORMAL) - GRID_FONT_SIZE * 0.3),
				ty + GRID_FONT_SIZE * 0.1, GRID_FONT_SIZE, .PREFER_NORMAL)
		}
	}

	def flushSmoothGeometry {
		_context.setPremultipliedBlendState
		_smooth.flush
	}

	def drawEquation(equation Equation.Node) {
		if equation == null {
			return
		}

		# Fall back to the cache for identical equations
		if _cachedEquation == null || !_cachedEquation.equals(equation) {
			var source = _compiler.compile(equation)

			# Store a copy of the equation for comparison next time
			_cachedEquation = equation.clone
			_equationMaterial = null

			# The compilation may fail, in which case we don't draw anything
			try {
				_equationMaterial = _context.createMaterial(SCREEN_QUAD_FORMAT, GLSLX_SOURCE_EQUATION_VERTEX,
					GLSLX_SOURCE_EQUATION_FRAGMENT.replaceAll("void", source + "void"))
			}

			# Report the error for debugging
			catch e dynamic {
				console.error(e, source)
			}
		}

		# Don't render anything if the compilation failed
		if _equationMaterial != null {
			_transformA.reset
			_transformA.scale(1 / _scale, -1 / scale)
			_transformA.translate(-_origin.x, -_origin.y)
			_transformA.scale(_platform.width * 0.5, -_platform.height * 0.5)
			_transformA.translate(1, -1)

			_equationMaterial.setUniformFloat(GLSLX_NAME_THICKNESS_AND_MODE,
				_compiler.hideCurve ? 0 : (_compiler.shadeAreaUnderCurve ? -2 : 2) * _platform.density)
			_equationMaterial.setUniformMat3(GLSLX_NAME_MATRIX3, _transformA)
			_equationMaterial.setUniformVec4(GLSLX_NAME_COLOR, 0, 0.5, 1, 1)
			_context.setPremultipliedBlendState
			_context.draw(.TRIANGLE_STRIP, _equationMaterial, _screenQuad)
		}
	}

	def flushText(color Color) {
		if _usedInstances == 0 {
			return
		}

		_transformA.reset
		_transformA.translate(-1, 1)
		_transformA.scale(2.0 / _platform.width, -2.0 / _platform.height)

		# Switch to the offscreen render target
		_context.setRenderTarget(_target)
		_context.setViewport(0, 0, _platform.pixelWidth, _platform.pixelHeight)
		_context.clear(.TRANSPARENT)
		_context.setAddBlendState

		# Take multiple samples of glyph outlines using the GPU
		var scale = 1 / _platform.density
		for i in 0.._usedInstances {
			var instance = _instances[i]
			if instance.glyph.area != null {
				for j in 0..JITTER_PATTERN.count {
					var offset = JITTER_PATTERN[j]
					_transformB.set(_transformA)
					_transformB.translate(instance.offset)
					_transformB.translate(offset.x * scale, offset.y * scale)
					_transformB.scale(instance.scale)
					if j % 2 == 0 {
						_glyphMaterial.setUniformVec4(GLSLX_NAME_COLOR, j == 0 ? 1 : 0, j == 2 ? 1 : 0, j == 4 ? 1 : 0, 0)
					}
					_glyphMaterial.setUniformMat3(GLSLX_NAME_MATRIX3, _transformB)
					_context.draw(.TRIANGLES, _glyphMaterial, instance.glyph.area)
				}
			}
		}

		# Composite the offscreen render target back onto the canvas using the non-zero winding rule
		_context.setRenderTarget(null)
		_context.setBlendState(.ZERO, .SOURCE_COLOR)
		_textMaterial.setUniformSampler(GLSLX_NAME_TEXTURE, _color, 0)
		_textMaterial.setUniformVec4(GLSLX_NAME_COLOR, .TRANSPARENT)
		_textMaterial.setUniformVec4(GLSLX_NAME_RECT,
			Math.floor(_instanceBounds.minX) / _platform.width,
			1 - Math.ceil(_instanceBounds.maxY) / _platform.height,
			Math.ceil(_instanceBounds.maxX) / _platform.width,
			1 - Math.floor(_instanceBounds.minY) / _platform.height)
		_context.draw(.TRIANGLE_STRIP, _textMaterial, _screenQuad)

		# Colors other than black must be rendered in two passes
		if color.opaque != .BLACK {
			_context.setAddBlendState
			_textMaterial.setUniformVec4(GLSLX_NAME_COLOR, color.redF, color.greenF, color.blueF, color.alphaF)
			_context.draw(.TRIANGLE_STRIP, _textMaterial, _screenQuad)
		}

		# Garbage collect temporary glyphs
		for i in 0.._usedInstances {
			var glyph = _instances[i].glyph
			if glyph.codePoint == Glyph.TEMPORARY_CODE_POINT {
				glyph.free
			}
		}

		# Reset the glyph instance buffer
		_usedInstances = 0
		_instanceBounds.reset
	}
}

namespace Canvas {
	const GRID_FONT_SIZE = 16
	const GRID_TICK_SIZE = 5
	const SELECTION_COLOR = Color.new(0, 127, 255, 63)
	const GRID_LIGHT_COLOR = Color.new(0, 0, 0, 31)
	const GRID_DARK_COLOR = Color.new(0, 0, 0, 127)
	const SCREEN_QUAD_FORMAT = GPU.VertexFormat.new.add(GLSLX_NAME_POSITION2, .FLOAT, 2)
	const GRID_SHOW_LINES_AT_SCALE = 800

	# 6x subpixel AA pattern
	#
	#   R = (f(x - 2/3, y) + f(x - 1/3, y) + f(x, y)) / 3
	#   G = (f(x - 1/3, y) + f(x, y) + f(x + 1/3, y)) / 3
	#   B = (f(x, y) + f(x + 1/3, y) + f(x + 2/3, y)) / 3
	#
	# The shader would require three texture lookups if the texture format
	# stored data for offsets -1/3, 0, and +1/3 since the shader also needs
	# data for offsets -2/3 and +2/3. To avoid this, the texture format stores
	# data for offsets 0, +1/3, and +2/3 instead. That way the shader can get
	# data for offsets -2/3 and -1/3 with only one additional texture lookup.
	#
	const JITTER_PATTERN = [
		Vector.new(-1 / 12.0, -5 / 12.0),
		Vector.new( 1 / 12.0,  1 / 12.0),
		Vector.new( 3 / 12.0, -1 / 12.0),
		Vector.new( 5 / 12.0,  5 / 12.0),
		Vector.new( 7 / 12.0, -3 / 12.0),
		Vector.new( 9 / 12.0,  3 / 12.0),
	]
}
