class App.OldTouch {
	const centerX double
	const centerY double
	const averageDistance double
	const touchCount int
	const time double
}

enum App.Animation {
	NONE
	DECAY
	EASE_IN_OUT
}

enum App.MouseAction {
	NONE
	CANVAS_MOVE
	EDITOR_SELECT
}

class App :: Platform.Delegate, Layout.Editor.Delegate {
	var _platform Platform
	var _oldPlatformWidth = 0.0
	var _oldPlatformHeight = 0.0
	var _canvas Canvas
	var _editor Layout.Editor
	var _fps = 60.0
	var _cursorToggleTime = 0.0
	var _isCursorVisible = true
	var _isInvalid = true
	var _idleFrames = 0
	var _oldTouches List<OldTouch> = []
	var _clipboard = Layout.Container.new
	var _originalMouseX = 0
	var _originalMouseY = 0
	var _previousMouseX = 0
	var _previousMouseY = 0
	var _mouseAction MouseAction = .NONE

	# Compilation support
	var _parser Equation.Parser = null
	var _checker Equation.Checker = null
	var _equation Equation.Node = null
	var _log = Equation.Log.new

	# Animation support
	var _animation Animation = .NONE
	var _startOrigin = Vector.new
	var _startScale = 0.0
	var _startTime = 0.0
	var _endOrigin = Vector.new
	var _endScale = 0.0
	var _endTime = 0.0

	def new(platform Platform, fontData DataArray) {
		_platform = platform
		_canvas = Canvas.new(platform, fontData)
		_editor = Layout.Editor.new(_canvas, EDITOR_PADDING, EDITOR_PADDING)
		_parser = Equation.Parser.new(_log, platform)
		_checker = Equation.Checker.new(_log)
		_createInitialContent
		_recompile
		_resetCursorBlink
		platform.setDelegate(self)
		_editor.setDelegate(self)
		handleResize
		_draw
	}

	def _createInitialContent {
		var route = _platform.route
		if route == null || route == "" {
			route = INITIAL_CONTENT[(Math.random * INITIAL_CONTENT.count) as int]
		}
		_editor.insert(Layout.Element.deserializeFromString(route))
		_editor.resetHistory
	}

	def handleFrame {
		var time = _platform.seconds
		if _animation != .NONE {
			var t = (time - _startTime) / (_endTime - _startTime)

			# Stop the animation once it's done
			if t > 1 {
				_canvas.setOriginAndScale(_endOrigin.x, _endOrigin.y, _endScale)
				_animation = .NONE
			}

			else {
				# Bend the animation curve for a more pleasant animation
				if _animation == .EASE_IN_OUT {
					t *= t * t * (t * (t * 6 - 15) + 10)
				} else {
					assert(_animation == .DECAY)
					t = 1 - t
					t = 1 - t * t * t * t * t
				}

				# Animate both origin and scale
				_canvas.setOriginAndScale(
					_startOrigin.x + (_endOrigin.x - _startOrigin.x) * t,
					_startOrigin.y + (_endOrigin.y - _startOrigin.y) * t,
					1 / (1 / _startScale + (1 / _endScale - 1 / _startScale) * t))
			}

			_invalidate
		}

		# Cursor invalidations don't count as user activity
		var shouldResetIdleFrames = _isInvalid

		# Toggle the cursor periodically
		if _platform.hasFocus && time > _cursorToggleTime && !_editor.hasSelection {
			_cursorToggleTime = time + CURSOR_BLINK_DELAY
			_isCursorVisible = !_isCursorVisible
			_invalidate
		}

		# Always draw immediately when invalid
		if _isInvalid {
			if shouldResetIdleFrames {
				_idleFrames = 0
			}
			_isInvalid = false
			_draw
		}

		# Render occasionally even when idle. Chrome must render at least 10fps to
		# avoid stutter when starting to render at 60fps again.
		else if _platform.hasFocus && ++_idleFrames % 6 == 0 && _idleFrames < 60 * 2 {
			_draw
		}
	}

	def handleScroll(deltaX double, deltaY double) {
		_stopAnimation
		_canvas.translateOrigin(-deltaX, -deltaY)
		_invalidate
	}

	def handlePinch(x double, y double, delta double) {
		_stopAnimation
		_canvas.adjustScaleWithPin(x, y, Math.pow(0.98, delta))
		_invalidate
	}

	def handleResize {
		_canvas.translateOrigin((_platform.width - _oldPlatformWidth) / 2, (_platform.height - _oldPlatformHeight) / 2)
		_oldPlatformWidth = _platform.width
		_oldPlatformHeight = _platform.height
		_stopAnimation
		_draw
	}

	def handleTouchStart(centerX double, centerY double, averageDistance double, touchCount int) {
		_stopAnimation
		_oldTouches.append(OldTouch.new(centerX, centerY, averageDistance, touchCount, _platform.seconds))
	}

	def handleTouchMove(centerX double, centerY double, averageDistance double, touchCount int) {
		var previous = _oldTouches.last
		if previous.touchCount == touchCount {
			if averageDistance != 0 && previous.averageDistance != 0 {
				_canvas.adjustScaleWithPin(previous.centerX, previous.centerY, averageDistance / previous.averageDistance)
			}
			_canvas.translateOrigin(centerX - previous.centerX, centerY - previous.centerY)
			_invalidate
		}
		_oldTouches.append(OldTouch.new(centerX, centerY, averageDistance, touchCount, _platform.seconds))
	}

	def handleTouchEnd(centerX double, centerY double, averageDistance double, touchCount int) {
		if touchCount != 0 {
			_oldTouches.append(OldTouch.new(centerX, centerY, averageDistance, touchCount, _platform.seconds))
			return
		}

		var count = _oldTouches.count
		var time = _platform.seconds

		# Search for an old touch that was long enough ago that the velocity should be stable
		for i = count - 2; i >= 0; i-- {
			var before = _oldTouches[i]

			# Ignore touches due to a pinch gesture
			if before.touchCount > 1 {
				break
			}

			# Ignore old touches that are too recent
			if time - before.time > 0.1 {
				var after = _oldTouches[i + 1]
				var scale = 1 / (after.time - before.time)
				var velocity = Vector.new((after.centerX - before.centerX) * scale, (after.centerY - before.centerY) * scale)
				var speed = velocity.length
				var duration = Math.log(1 + speed) / 5
				var flingDistance = speed * duration / 5 # Divide by 5 since a quintic decay function has an initial slope of 5

				# Only fling if the speed is fast enough
				if speed > 50 {
					_startAnimation(.DECAY, duration)
					_endOrigin += velocity * (flingDistance / speed)
				}
				break
			}
		}

		_oldTouches = []
	}

	def handleMouseDown(x int, y int, buttons Platform.Buttons) {
		if _mouseAction == .NONE {
			if isOverEditor(x, y) {
				_mouseAction = .EDITOR_SELECT
				_originalMouseX = x
				_originalMouseY = y
				handleMouseMove(x, y, buttons)
			}

			else {
				_mouseAction = .CANVAS_MOVE
				_platform.setCursor(.MOVE)
			}
		}

		_previousMouseX = x
		_previousMouseY = y
	}

	def handleMouseMove(x int, y int, buttons Platform.Buttons) {
		if _mouseAction == .CANVAS_MOVE {
			_canvas.translateOrigin(x - _previousMouseX, y - _previousMouseY)
			_platform.setCursor(.MOVE)
			_invalidate
		}

		else if _mouseAction == .EDITOR_SELECT {
			_platform.setCursor(.TEXT)
			_editor.selectRange(_originalMouseX, _originalMouseY, x, y)
		}

		else {
			assert(_mouseAction == .NONE)
			_platform.setCursor(isOverEditor(x, y) ? .TEXT : .DEFAULT)
		}

		_previousMouseX = x
		_previousMouseY = y
	}

	def handleMouseUp(x int, y int, buttons Platform.Buttons) {
		if buttons == 0 {
			_mouseAction = .NONE
			_platform.setCursor(isOverEditor(x, y) ? .TEXT : .DEFAULT)
		}

		_previousMouseX = x
		_previousMouseY = y
	}

	def isOverEditor(x int, y int) bool {
		return
			x < _editor.box.width + 2 * EDITOR_PADDING &&
			y < _editor.box.height + 2 * EDITOR_PADDING
	}

	def handleShortcut(shortcut Platform.Shortcut) {
		switch shortcut {
			case .UNDO { _editor.undo }
			case .REDO { _editor.redo }
			case .SELECT_ALL { _editor.selectAll }
			case .CUT { _clipboard = _editor.cut }
			case .COPY { _clipboard = _editor.copy }
			case .PASTE { _editor.insert(_clipboard) }

			case .MOVE_LEFT_CHARACTER { _editor.move(.LEFT, .CHARACTER, .RESET) }
			case .MOVE_RIGHT_CHARACTER { _editor.move(.RIGHT, .CHARACTER, .RESET) }
			case .MOVE_LEFT_WORD { _editor.move(.LEFT, .WORD, .RESET) }
			case .MOVE_RIGHT_WORD { _editor.move(.RIGHT, .WORD, .RESET) }
			case .MOVE_LEFT_LINE { _editor.move(.LEFT, .LINE, .RESET) }
			case .MOVE_RIGHT_LINE { _editor.move(.RIGHT, .LINE, .RESET) }
			case .MOVE_UP_LINE { _editor.move(.LEFT, .LINE, .RESET) }
			case .MOVE_DOWN_LINE { _editor.move(.RIGHT, .LINE, .RESET) }
			case .MOVE_UP_DOCUMENT { _editor.move(.LEFT, .DOCUMENT, .RESET) }
			case .MOVE_DOWN_DOCUMENT { _editor.move(.RIGHT, .DOCUMENT, .RESET) }

			case .EXTEND_LEFT_CHARACTER { _editor.move(.LEFT, .CHARACTER, .EXTEND) }
			case .EXTEND_RIGHT_CHARACTER { _editor.move(.RIGHT, .CHARACTER, .EXTEND) }
			case .EXTEND_LEFT_WORD { _editor.move(.LEFT, .WORD, .EXTEND) }
			case .EXTEND_RIGHT_WORD { _editor.move(.RIGHT, .WORD, .EXTEND) }
			case .EXTEND_LEFT_LINE { _editor.move(.LEFT, .LINE, .EXTEND) }
			case .EXTEND_RIGHT_LINE { _editor.move(.RIGHT, .LINE, .EXTEND) }
			case .EXTEND_UP_LINE { _editor.move(.LEFT, .LINE, .EXTEND) }
			case .EXTEND_DOWN_LINE { _editor.move(.RIGHT, .LINE, .EXTEND) }
			case .EXTEND_UP_DOCUMENT { _editor.move(.LEFT, .DOCUMENT, .EXTEND) }
			case .EXTEND_DOWN_DOCUMENT { _editor.move(.RIGHT, .DOCUMENT, .EXTEND) }

			case .DELETE_LEFT_CHARACTER { _editor.delete(.LEFT, .CHARACTER) }
			case .DELETE_RIGHT_CHARACTER { _editor.delete(.RIGHT, .CHARACTER) }
			case .DELETE_LEFT_WORD { _editor.delete(.LEFT, .WORD) }
			case .DELETE_RIGHT_WORD { _editor.delete(.RIGHT, .WORD) }
			case .DELETE_LEFT_LINE { _editor.delete(.LEFT, .LINE) }
			case .DELETE_RIGHT_LINE { _editor.delete(.RIGHT, .LINE) }
		}
	}

	def handleCodePoint(c int) {
		var insertion = Layout.Container.new

		# Fractions
		if c == '/' {
			_editor.selectWordBeforeSelection
			var special = Layout.Special.new(.FRACTION)
			var selection = _editor.copy
			special.children.append(selection)
			special.children.append(Layout.Container.new)
			insertion.children.append(special)
			_editor.insert(insertion)
			_editor.move(.LEFT, .CHARACTER, .RESET)
			if selection.children.isEmpty {
				_editor.move(.LEFT, .CHARACTER, .RESET)
			}
		}

		# Closing absolute value
		else if c == '|' && !_editor.hasSelection && _editor.isAtEndOf(.ABSOLUTE_VALUE) && _editor.isBeforeOperand {
			_editor.move(.RIGHT, .CHARACTER, .RESET)
		}

		# Closing brackets
		else if (c == ']' || c == ')') && !_editor.hasSelection && _editor.isAtEndOf(c == ']' ? .BRACKETS : .PARENTHESES) {
			_editor.move(.RIGHT, .CHARACTER, .RESET)
		}

		# Closing brackets
		else if (c == ']' || c == ')') && !_editor.hasSelection && _editor.isBeforeOperand {
			_editor.move(.LEFT, .WORD, .EXTEND)
			var special = Layout.Special.new(c == ']' ? .BRACKETS : .PARENTHESES)
			special.children.append(_editor.copy)
			insertion.children.append(special)
			_editor.insert(insertion)
		}

		# Opening brackets (and closing brackets when the cases above don't apply)
		else if c == '^' || c == '_' || c == '√' || c == '|' || c == '[' || c == ']' || c == '(' || c == ')' {
			var special = Layout.Special.new(
				c == '^' ? .OVER :
				c == '_' ? .UNDER :
				c == '√' ? .SQUARE_ROOT :
				c == '|' ? .ABSOLUTE_VALUE :
				c == '[' || c == ']' ? .BRACKETS :
				.PARENTHESES)
			var selection = _editor.copy
			special.children.append(selection)
			insertion.children.append(special)
			_editor.insert(insertion)
			_editor.move(.LEFT, .CHARACTER, .RESET)
			if !selection.children.isEmpty {
				_editor.move(.LEFT, .LINE, .EXTEND)
			}
		}

		# Normal characters
		else {
			insertion.children.append(Layout.Character.new(c))
			_editor.insert(insertion)
		}
	}

	def handleFocusChange {
		if _platform.hasFocus {
			_resetCursorBlink
			_mouseAction = .NONE
		}

		else if _isCursorVisible {
			_isCursorVisible = false
			_invalidate
		}
	}

	def handleEditorChange(change Layout.Editor.Change) {
		_resetCursorBlink
		if change == .CONTENTS {
			_recompile
		}
	}

	def _recompile {
		var container = _editor.cloneContents
		_log.clear
		_platform.setRoute(container.serializeToString)
		_equation = _parser.parse(container)
		if !_log.hasErrors {
			_checker.check(_equation)
		}
		if _log.hasErrors {
			_equation = null
		}
	}

	def _invalidate {
		_isInvalid = true
	}

	def _startAnimation(animation Animation, duration double) {
		_animation = animation

		# Configure the start
		_startOrigin.x = _canvas.originX
		_startOrigin.y = _canvas.originY
		_startScale = _canvas.scale
		_startTime = _platform.seconds

		# Configure the end
		_endOrigin.set(_startOrigin)
		_endScale = _startScale
		_endTime = _startTime + duration
	}

	def _stopAnimation {
		_animation = .NONE
	}

	def _resetCursorBlink {
		_cursorToggleTime = _platform.seconds + CURSOR_BLINK_DELAY
		_isCursorVisible = true
		_invalidate
	}

	def _draw {
		_canvas.beginFrame
		_canvas.drawGrid
		_canvas.flushSmoothGeometry
		_canvas.drawEquation(_equation)
		_editor.render(_isCursorVisible ? .SHOW_CURSOR : .HIDE_CURSOR)
		_canvas.flushText(.BLACK)
		_canvas.flushSmoothGeometry

		if _log.hasErrors {
			var error = _log.errors.first
			_editor.renderErrorForRange(error.range.start, error.range.end)
			_canvas.addText("Error: " + error.message, EDITOR_PADDING, _editor.box.y + _editor.box.height + EDITOR_PADDING, Layout.Element.MIN_FONT_SIZE, .PREFER_NORMAL)
			_canvas.flushText(.RED)
			_canvas.flushSmoothGeometry
		}

		_canvas.endFrame
	}
}

namespace App {
	const CURSOR_BLINK_DELAY = 0.5
	const EDITOR_PADDING = 20

	const INITIAL_CONTENT = [
		"(x^2-y^2)^2=4^2",
		"abs(x^2-1)=abs(y^2-1)",
		"asintanhy=sinx",
		"cos(x-sin2y)≤cos(y-sin2x)",
		"r=2sin7(θ+sinr)",
		"sin(2lnr+θ)≥0",
		"sin(x-cosπy)≤sin(y+cosπx)",
		"sin7θ≥sinr",
		"sinsqrt(x^2+y^2)+cosx+cosy≤0",
		"sqrt(x^2+y^2)-cosπx-cosπy≤y",
		"x-sinπy≤y+sinπx",
		"x^2+y^2=4",
		"y=(sinπx)/x",
		"y=abs(x+2)+abs(x-2)-abs(x)-2",
		"y=sin(15x+10sinx)",
		"y=sin(3x+sin4(y+sin2(x+siny)))",
		"y=x^3-x",
		"ysqrt(y^2+x^2)=x",
		"πy=sinπx+sinπy",
	]

	@entry
	def main {
		var platform = HTMLPlatform.new
		if platform.context == null {
			document.write("This application requires WebGL.")
		} else {
			platform.fetchResource("fonts.bin", bytes => new(platform, DataArray.new(bytes)))
		}
		console.log("Hello")
	}
}
